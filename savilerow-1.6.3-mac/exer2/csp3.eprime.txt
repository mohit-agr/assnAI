language ESSENCE' 1.0
letting NBRPPL = 9

$ Each person must exactly be in one group
	find Frodo, Sam, Merry, Pippin, Aragorn, Legolas, Gimli, Gandalf, Boromir: int(1..3)
	find M: matrix indexed by [int(1..NBRPPL)] of int(1..3)
such that
	$ each person must be in exactly one group
		M[1] = Frodo,
		M[2] = Sam,
		M[3] = Merry,
		M[4] = Pippin,
		M[5] = Aragorn,
		M[6] = Legolas,
		M[7] = Gimli,
		M[8] = Gandalf,
		M[9] = Boromir,

	$ each group contains between two and four people
		atleast(M, [2,2,2], [1,2,3]),
		atmost(M, [4,4,4], [1,2,3]),

	$ Frodo must be in the same group as Sam
		Frodo = Sam,

	$ Legolas is not allowed to be in the same group as Gimli
		Legolas != Gimli,

	$ Merry, Pippin, Frodo and Sam will not cook breakfast
		Merry != 1,
		Pippin != 1,
		Frodo != 1,
		Sam != 1,

	$ Aragorn and Boromir will not cook dinner
		Aragorn != 3,
		Boromir != 3,

	$ Merry is not allowed to be in the same group as Pippin
		Merry != Pippin,

	$ Gimli must always cook after Merry
		Gimli > Merry,

	$ Frodo must always cook after Merry
	 	Frodo > Merry,

	$ Aragorn is not allowed to be in the same group as Boromir, Legolas or Gandalf
		Aragorn != Boromir,
		Aragorn != Legolas,
		Aragorn != Gandalf
